BUS JkOrderBus ver 0.2 from 百灵威系统工程部/[jk-order]
ACCEPT order {
	VAR mainId ID, row INT;
	SET mainId = id;
	WITH OrderMain as a ID=id SET a.no=no, a.customer=customer;
	SET row = 1;
	FOR detail {
		WITH OrderDetail as a 
			ID = id
			SET a.main=mainId, a.row=row
				, a.item=item, a.product=product
				, a.quantity=quantity, a.amount=amount, a.price=price;
		SET row=row+1;
	}
}
ACCEPT deliver {
	VAR customer ID;
	SET customer = a.customer FROM OrderMain as a WHERE a.id=id;
	FOR detail {
		WITH IxPendingDeliver ixx=warehouse ix=customer xi=id;
		WITH DxOrderDetail as a ID=id SET a.deliver=value, a.deliverCenter=warehouse;
	}
}
ACCEPT [return] {
	VAR customer ID;
	SET customer = a.customer FROM OrderMain as a WHERE a.id=id;
	FOR detail {
		VAR deliver DEC(12,2), deliverDone DEC(12,2), returnDone DEC(12,2), deliverCenter ID, retQuantity DEC(12,2);
		SET deliver=IFNULL(a.deliver, 0)
			, deliverDone=IFNULL(a.deliverDone, 0)
			, returnDone=IFNULL(a.returnDone, 0)
			, deliverCenter=a.deliverCenter
			FROM DxOrderDetail as a WHERE a.id=[order-detail-id];
		WITH DxReturnDetail as a ID=id SET a.orderDetail=[order-detail-id], a.quantity=quantity;
		-- 做return相关操作
		IF exists(SELECT xi FROM IxPendingDeliver WHERE ixx=deliverCenter AND ix=customer AND xi=id) {
			SET retQuantity = quantity;
			WITH DxOrderDetail ID=id SET returnDone=retQuantity;
			IF deliver - deliverDone - returnDone - retQuantity <= 0 {
				WITH IxPendingDeliver IXX=deliverCenter IX=customer XI=id DEL;
				WITH DxReturnDetail as a ID=id SET a.quantityDone=retQuantity;
			}
		}
		ELSE {
			WITH IxCustomerPendingReturn ix=customer xi=id;
			SET retQuantity = 0;
		}
		Bus JkOrderBus.[done-return-deliver] INTO detail
			ADD id=id, value=retQuantity;
	}
};
