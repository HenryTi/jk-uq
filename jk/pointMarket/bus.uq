bus commonBus from 百灵威系统工程部/common;

/*
 * 接收从内部系统导入的积分
*/
BUS pointBus from [百灵威系统工程部]/pointShop
    ACCEPT customerPoint 
    BUS webUserBus.getWuByCu as q into tb {
        set q.customer = customer;
    } {
        -- 取消从内部系统导入PointBookByCustomer流程
        -- book PointBookByCustomer at (customer, pointYear) set point = point, totalPoint = totalPoint, usedPoint = usedPoint;

        -- 根据customer获取webuser
        var webUserid ID [$user];
        foreach tb {
            set webUserid = webUser;
        }
        -- 同时将临时积分清空
        if( not webUserid is null ){
            book PointBook at (webUserid, pointYear) set point = point, totalPoint = totalPoint, usedPoint = usedPoint,
            tempPoint = 0;
        }
    }
    /*
    * 在joint中发送bus，此处接收，用于将积分可兑换产品从老系统同步到tonva
    * 2020/11/10, 此bus作废不再使用
    */
    ACCEPT pointProductBus {
        var pointProductLibId int;
        set pointProductLibId = pointProduct from PointProductSource where sourceId = pack and type = 'self';
        if (pointProductLibId is null) {
            TUID pointProductLib into pointProductLibId set description = description, descriptionC = descriptionC,
                grade = grade, point = point, startDate = from_unixtime(startDate), endDate = from_unixtime(endDate), imageUrl = imageUrl,
                isValid = isValid; 
            BOOK PointProductSource at(pointProductLibId, pack) set type = 'self';
        } else {
            TUID pointProductLib id(pointProductLibId) set description = description, descriptionC = descriptionC,
                grade = grade, point = point, startDate = from_unixtime(startDate), endDate = from_unixtime(endDate), imageUrl = imageUrl,
                isValid = isValid; 
        }
    };

/*
 * webUser使用了credits后，删除该webUser领用的credits
*/
bus couponBus from 百灵威系统工程部/coupon
    ACCEPT creditsUsedByWebUser 
    BUS webUserBus.getCuByWu as q into tb {
        set q.webUser = webUser;
    }{
        var pointYear int;
        set pointYear = YEAR(NOW());
        delete a from WebUserCredits as a where a.webUser = webUser and a.credits = coupon;
        book WebUserCreditsUsed at (webUser, coupon, orderId ) set  usedDate = now();
        book PointBook at (webUser, pointYear) set tempPoint += point;
        history PointHistory
            set webUser = webuser, pointYear = pointYear, source = 1, point = point, comments = orderid;

        --积分券下单添加客户积分
        var customerId int;
        foreach tb {
            set customerId = customer;
        }
        if(customerId > 0){
            book PointBookByCustomer at (customerId, pointYear) set totalpoint += point;
            history PointHistoryByCustomer 
                set pointYear = pointYear, point = point, source = 1, comments = orderid, customer = customerId;
        }
    };

BUS webUserBus from [百灵威系统工程部]/WebUser
    ACCEPT WebUserAuditPass
    {
        -- 审核后Customer的积分导入到webuser
        if(not exists(select 1 from PointBook as pb where pb.webUser = webUser)) {
            foreach (
                var pointYear int, point int, usedPoint int ,totalPoint int
                of select a.pointYear, a.point , a.usedPoint, a.totalPoint
                from PointBookByCustomer as a where a.customer = customer
            ) {
                book PointBook at (webUser, pointYear) set  point = point, usedPoint = usedPoint, totalPoint = totalPoint;
            }
        }

        -- 把webuser领用的积分券导入内部
        bus couponBus.creditsDrawedByCustomer set customer = customer, webUser = webUser;
        foreach (
            var credits int, createDate date, expiredDate date 
            of select a.credits, a.createDate, a.expiredDate
            from WebUserCredits as a 
            where a.webUser = webUser and a.expiredDate > now()
        ) {
            bus couponBus.creditsDrawedByCustomer into coupons add coupon = credits, 
                createDate = createDate, expiredDate = expiredDate;
        }

        -- 审核后webUser的签到积分导入到内部
        foreach(
            var pointYear2 int, signInPoint int
            of  select pointYear2, sum(h.point) as signInPoint from PointHistory as h 
            where h.webUser = webUser and h.pointYear > year(now()) - 3 and h.source = 2
            group by h.pointYear as pointYear2
        ) {
            bus ps2Bus.customerSignIn set customer = customer, webUser = webUser, pointYear = pointYear2, point = signInPoint, comments = '';
        }
    };

BUS orderChangedBus from [百灵威系统工程部]/[orderChanged]
    ACCEPT orderChanged(
            orderMainId, orderDetailId, orderId, orderItemId, seller, salesman, salesRegion, customer, buyerAccount, organization, brand, product, pack, quantity, price,
            currency as pricecurrency, retail, retailCurrency, bottomPrice, bottomPriceCurrency, costPrice, costPriceCurrency, mark, tradeType, taxRate, promotionId, createDate
        )
        BUS ps2Bus.getPointMultiple as q into tb {
            set q.createDate = createDate;
        }
        BUS commonBus.getRmbRate as q2 into tb2 {
            set q2.year = year(date(now()));
            set q2.month = month(date(now()));
            into q2.currencies select pricecurrency as currency;
            into q2.currencies select retailCurrency as currency;
        } {
        var multiple int;
        foreach tb {
            set multiple = multiple;
        }

        var priceRMBRate dec(12, 4), retailRMBRate dec(12, 4);
        foreach tb2.currencyRate {
            if(retailCurrency = currency){
                set retailRMBRate = rate;
            }
            if(pricecurrency = currency){
                set priceRMBRate = rate;
            }      
        }

        if (mark <> 'N' and mark <> 'N1' and mark <> 'PR' and retailCurrency = 5 and tradeType = 'a'){
            var rTotalPoint int;
            set rTotalPoint = a.totalPoint
            from    OrderBack as a
            where   a.orderItemId = orderItemId;
        
            set rTotalPoint = ifnull(rTotalPoint, 0);
            /*
            var currentDate date, currentYear int, currentMonth int;
            set currentDate = date(now());
            set currentYear = year(currentDate), currentMonth = month(currentDate);
            
            var priceRMB dec(12, 2), retailRMB dec(12, 2);
            set priceRMB = price * a.RMBTo 
            from    CurrencyExchangeRate as a
            where   a.currency = currency and a.year = currentYear and a.month = currentMonth; 
            set retailRMB = retail * a.RMBTo 
            from    CurrencyExchangeRate as a
            where   a.currency = retailCurrency and a.year = currentYear and a.month = currentMonth; 

            set price = priceRMB, retail = retailRMB;
            */

            set price = price * priceRMBRate, retail = retail * retailRMBRate;

            var dDiscount dec(12, 2);
            set dDiscount = b.discount
            from    BrandMinDiscount as b
            where   b.brand = brand;

            -- 计算积分差
            var nTotalPoint int, diffPoint int;
            if(mark = 'C' or (price < (retail * ifnull(1 - dDiscount, 1)-1))) {
                set nTotalPoint = 0;
            } else {
                set nTotalPoint = price * quantity * ifnull(multiple, 1);
            }
            set diffPoint = nTotalPoint - rTotalPoint;

            var pointYear int;
            set pointYear = year(from_unixtime(createDate));
            if(diffPoint <> 0) {
                book PointBookByCustomer at (customer, pointYear) set totalpoint += diffPoint;
                history PointHistoryByCustomer 
                    set pointYear = pointYear, point = diffPoint, source = 5, comments = concat(OrderID, ',', orderItemId), customer = customer;
            }

            book orderBack at(orderItemId) set orderId = orderId, customer = customer, 
                brand = brand, product = product, pack = pack, quantity = quantity, price = price,
                currency = pricecurrency, retail = retail, retailCurrency = retailCurrency, totalPoint += diffPoint,
                mark = mark, createDate = from_unixtime(createDate);

        }
    };

BUS orderPaiedBus from [百灵威系统工程部]/[FiReceivable]
    ACCEPT innerOrderPaid {
        /*
        -- 测试数据 orderItemId找不到时的固定值
        set orderItemId = 'LCW1742012231406135837527', amount = 744;
        */
        if not exists (select 1 from OrderBack as w where w.orderItemId = orderItemId){
            var pid ID;
            PENDING SOrderPaymentPending + ( orderId:orderId, orderItemId:orderItemId, amount:amount, currency:currency, 
                action:action) to pid;
        }

        var pointYear int; --年度取值为最新的总积分年度
        var totalPoint int, customer ID Customer,price dec(12, 2), quantity int;
        set totalPoint = a.totalPoint, price = a.price, quantity = a.quantity, customer = a.customer, pointYear = year(a.createDate)
        from    OrderBack as a
        where   a.orderItemId = orderItemId;

        /*
            -- 因合同币种=开票币种=回款币种，货币转换 暂时不实现
            var currentDate date, currentYear int, currentMonth int;
            set currentDate = date(now());
            set currentYear = year(currentDate), currentMonth = month(currentDate);
            
            var amountRMB dec(12, 2);
            set amountRMB = amount * a.RMBTo 
            from    CurrencyExchangeRate as a
            where   a.currency = currency and a.year = currentYear and a.month = currentMonth; 
            set amount = amountRMB;
        */
    
        var realPoint int;
        if(amount / (price * quantity) >= 1){
            set realPoint = 1 * totalPoint; 
        }else{
            set realPoint = amount / (price * quantity) * totalPoint;  --按订单明细的回款占比*订单明细的总积分计算订单明细的有效积分
        }

        if(action <> 1) {
            set realPoint = realPoint * -1;   --有效积分取消
        }
        book PointBookByCustomer at (customer, pointYear) set point += realPoint;
    };

    BUS ps2Bus from [百灵威系统工程部]/pointShop2
    ACCEPT pointInitialization {
        --订单积分
        if(source=5){
            
            book orderBack at(orderItemId) set orderId = orderId, customer = customer, 
                brand = brand, product = product, pack = pack, quantity = quantity, price = price,
                currency = currency, retail = retail, retailCurrency = retailCurrency, totalPoint += mscore,
                mark = mark, createDate = createDate;

            --总积分
            book PointBookByCustomer at (customer, pointYear) set totalpoint += mscore;
            history PointHistoryByCustomer 
                set pointYear = pointYear, point = mscore, source = 5, comments = comments, customer = customer;

            --有效积分
            if(isvalid=1){
                book PointBookByCustomer at (customer, pointYear) set point += mscore;
            }

        }else if(source=2){  --签到积分

            book PointBookByCustomer at (customer, pointYear) set point += mscore, totalpoint += mscore;
            history PointHistoryByCustomer 
                set pointYear = pointYear, point = mscore, source = 2, comments = comments, customer = customer; 

        }else if(source=3){  --积分兑换

            book PointBookByCustomer at (customer, pointYear) set usedPoint += mscore;
            history PointHistoryByCustomer 
                set pointYear = pointYear, point = (0-mscore), source = 3, comments = comments, customer = customer; 

        }
        /*
        if(isvalid=1){
            if exists (select 1 from PointBookByCustomer as e where e.customer=customer and e.pointYear=pointYear){
                book PointBookByCustomer at (customer, pointYear) set point += mscore;
            }
        }*/
    };