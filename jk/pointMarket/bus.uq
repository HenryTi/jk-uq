bus commonBus from 百灵威系统工程部/common;

/*
 * 接收从内部系统导入的积分
*/
BUS pointBus from [百灵威系统工程部]/pointShop
    ACCEPT customerPoint 
    BUS webUserBus.getWuByCu as q into tb {
        set q.customer = customer;
    } {
        -- 根据customer获取webuser
        var webUserid ID [$user];
        foreach tb {
            set webUserid = webUser;
        }
        -- 同时将临时积分清空
        if( not webUserid is null ){
            book PointBook at (webUserid, pointYear) set point = point, totalPoint = totalPoint, usedPoint = usedPoint,
            tempPoint = 0;
        }
    }
    /*
    * 在joint中发送bus，此处接收，用于将积分可兑换产品从老系统同步到tonva
    * 2020/11/10, 此bus作废不再使用
    */
    ACCEPT pointProductBus {
        var pointProductLibId int;
        set pointProductLibId = pointProduct from PointProductSource where sourceId = pack and type = 'self';
        if (pointProductLibId is null) {
            TUID pointProductLib into pointProductLibId set description = description, descriptionC = descriptionC,
                grade = grade, point = point, startDate = from_unixtime(startDate), endDate = from_unixtime(endDate), imageUrl = imageUrl,
                isValid = isValid; 
            BOOK PointProductSource at(pointProductLibId, pack) set type = 'self';
        } else {
            TUID pointProductLib id(pointProductLibId) set description = description, descriptionC = descriptionC,
                grade = grade, point = point, startDate = from_unixtime(startDate), endDate = from_unixtime(endDate), imageUrl = imageUrl,
                isValid = isValid; 
        }
    };

/*
 * webUser使用了credits后，删除该webUser领用的credits
*/
bus couponBus from 百灵威系统工程部/coupon
    ACCEPT creditsUsedByWebUser 
    BUS webUserBus.getCuByWu as q into tb {
        set q.webUser = webUser;
    }{
        var pointYear int;
        set pointYear = YEAR(NOW());
        book PointBook at (webUser, pointYear) set tempPoint += point;
        history PointHistory
            set webUser = webuser, pointYear = pointYear, source = 1, point = point, comments = orderid;

        /*
        --积分券下单添加客户积分
        var customerId int;
        foreach tb {
            set customerId = customer;
        }
        if(customerId > 0){
            book PointBookByCustomer at (customerId, pointYear) set totalpoint += point;
            history PointHistoryByCustomer 
                set pointYear = pointYear, point = point, source = 1, comments = orderid, customer = customerId;
        }
        */
    };

BUS webUserBus from [百灵威系统工程部]/WebUser
    ACCEPT WebUserAuditPass {
        /*
        -- 审核后Customer的积分导入到webuser
        if(not exists(select 1 from PointBook as pb where pb.webUser = webUser)) {
            foreach (
                var pointYear int, point int, usedPoint int ,totalPoint int
                of select a.pointYear, a.point , a.usedPoint, a.totalPoint
                from PointBookByCustomer as a where a.customer = customer
            ) {
                book PointBook at (webUser, pointYear) set  point = point, usedPoint = usedPoint, totalPoint = totalPoint;
            }
        }
        */

        -- 审核后webUser的签到积分导入到内部
        foreach(
            var pointYear2 int, signInPoint int
            of  select pointYear2, sum(h.point) as signInPoint from PointHistory as h 
            where h.webUser = webUser and h.pointYear > year(now()) - 3 and h.source = 2
            group by h.pointYear as pointYear2
        ) {
            bus ps2Bus.customerSignIn set customer = customer, webUser = webUser, pointYear = pointYear2, point = signInPoint, comments = '';
        }
    };

/*
订单总积分计算
*/
BUS poBus from 百灵威系统工程部/PointOrder
    ACCEPT orderPointChanged
        BUS ps2Bus.getCreditsMultiple as a into cm {
            set a.credits = coupon;
        }
        BUS ps2Bus.getPromotionMultiple as a into pm {
            set a.endUser = customer, a.brand = brand, a.price = price, a.retail = retail, a.createDate = createDate;
        }
    {   
        --积分年度
        var pointYear int ;
        set pointYear = year(from_unixtime(createDate));
        
        -- 客户是积分黑名单客户，则不参与积分。
        var bCustomer int;
        set bCustomer= a.blackCustomer
        from PointCustomerBlacklist as a
        where a.blackCustomer=customer;
        set bCustomer = ifnull(bCustomer, 0);

        -- 1.积分调整
        -- 1.1，积分最终用户改变
        var lastEnduser int;        --上次订单积分最终用户
        var lastTotalPoint int;     --上次订单积分值
        set lastEnduser = b.endUser, lastTotalPoint = a.totalPoint
        from    DxOrderDetail as a
                join OrderDetail as b on a.id = b.id
        where a.id=orderDetailId;
        set lastEnduser = ifnull(lastEnduser,0);

        if ( lastEnduser <> 0 and lastEnduser <> customer and pointYear > 2020) {
                -- 取消上次订单客户记账
                book PointBookByCustomer at (lastEnduser, pointYear) set totalpoint += -lastTotalPoint;
                -- 上次订单客户积分，取消历史记录
                history PointHistoryByCustomer 
                    set pointYear = pointYear, point = -lastTotalPoint, source = 5, 
                    comments = concat(orderMainNo, ',', orderDetailNo), customer = lastEnduser;
        }

        -- 订单积分条件的判定
        -- 销售币种为RMB、国内正常销售业务,非积分黑名单客户，允许积分
        if (currency = 5 and tradeType = 'a' and mark <> 'PR' and bCustomer = 0 and pointYear > 2020){

            var multiple int, creditsMultiple int, promotionMultiple int;
            foreach cm {
                set creditsMultiple = multiple;
            }
            set creditsMultiple = ifnull(creditsMultiple, 0);
            
            foreach pm {
                set promotionMultiple = multiple;
            }
            set promotionMultiple = ifnull(promotionMultiple, 0);

            if (creditsMultiple > promotionMultiple) {
                set multiple = creditsMultiple;
            } else {
                set multiple = promotionMultiple;
            }

            var lTotalPoint int, diffPoint dec(12, 2);
            -- mark为C：0积分
            if (mark = 'C'){
                set lTotalPoint = 0;
            } else {
                set lTotalPoint = price * quantity * multiple;
            }

            -- 获取订单积分记录
            -- 计算积分差值
            var rTotalPoint int;
            set rTotalPoint = a.totalPoint
            from    DxOrderDetail as a
            where   a.id = orderDetailId;

            set diffPoint = lTotalPoint - ifnull(rTotalPoint, 0);

            -- 积分最终客户变动，差异积分+在上次积分值，无变动差值不做调整
            var cDiffPoint int;
            
             if ( lastEnduser <> 0 and lastEnduser <> customer) {
                 set cDiffPoint = diffPoint + ifnull(lastTotalPoint, 0);
             } else {
                 set cDiffPoint = diffPoint;
             }
            if (cDiffPoint <> 0){
                -- 1.积分记账
                -- 1.1. 总积分记账, 针对客户
                book PointBookByCustomer at (customer, pointYear) set totalpoint += cDiffPoint;

                -- 2.积分历史记录
                -- 2.1针对客户、年度、积分、积分来源分类、积分源ID(订单ID,积分券Id),备注（上次积分与本次积分计算过程）
                history PointHistoryByCustomer 
                    set pointYear = pointYear, point = cDiffPoint, source = 5, 
                    comments = concat(orderMainNo, ',', orderDetailNo), customer = customer;               
            }

            -- 积分差值非零值，记录本次积分
            if (diffPoint <> 0){
                -- 1.积分记账
                -- 1.2.订单积分记账，针对订单明细
                WITH DxOrderDetail as a ID = orderDetailId
                    set a.totalPoint += diffPoint, a.multiple = multiple;

                -- 2.积分历史记录
                -- 2.2 针对订单明细
                history OrderDetailPointHistory
                    set orderDetailId = orderDetailId, pointYear = pointYear, point = diffPoint, 
                        type = 5, comments = concat(orderMainNo, ',', orderDetailNo);
            }
        }

        -- 保存订单信息
        WITH OrderDetail as a id=orderDetailId
            set a.endUser = customer, a.createDate = from_unixtime(createDate)
                ,a.orderMainNo=orderMainNo, a.orderDetailNo = orderDetailNo;
    };


/*
订单有效积分计算
*/
BUS JkOrderBus from 百灵威系统工程部/[jk-order]
    -- 积分订单信息
    ACCEPT order {
        VAR mainId ID;
        SET mainId = id;
        WITH OrderMain as a ID = id SET a.no = no;
        FOR detail {
            WITH OrderDetail as a 
                ID = id
                SET a.main = mainId;
        }
    }
    -- 订单回款计算有效积分
    ACCEPT [receive-done] {
        FOR detail {
            var lValidPoint int;    -- 本次回款有效积分
            var pointYear int;      -- 积分年度
            var customer int;        -- 最终积分用户
            var rTotalPoint int;        -- 订单总积分
            var orderMainNo char(50);
            var orderDetailNo char(50);
            var orderDetailId int;

            if exists (select 1 from DxOrderDetail as dod where dod.id = id) {
                set lValidPoint = value * b.multiple
                    , customer = a.endUser, pointYear = year(a.createDate), rTotalPoint = b.totalPoint
                    , orderMainNo = a.orderMainNo, orderDetailNo = a.orderDetailNo
                    , orderDetailId = id
                from    OrderDetail as a
                        join DxOrderDetail as b on a.id = b.id
                where a.id = id;

                -- 如果本次回款有效积分大于订单总积分，则本次回款有效积分修改为订单总积分
                if (lValidPoint > rTotalPoint) {
                    set lValidPoint = rTotalPoint;
                }

                -- 1.有效积分记账
                -- 1.1. 针对客户
                book PointBookByCustomer at (customer, pointYear) set point += lValidPoint;
                -- 1.2.订单积分记账，针对订单明细
                WITH DxOrderDetail as a ID = orderDetailId set a.point += lValidPoint;

                -- 2.积分历史记录
                -- 2.1针对客户、年度、积分、积分来源分类、积分源ID(订单ID,积分券Id),备注（上次积分与本次积分计算过程）
                history PointHistoryByCustomer 
                    set pointYear = pointYear, point = lValidPoint, source = 6, 
                    comments = concat(orderMainNo, ',', orderDetailNo), customer = customer;               
                -- 2.2 针对订单明细
                history OrderDetailPointHistory
                    set orderDetailId = orderDetailId, pointYear = pointYear, point = lValidPoint, 
                    type = 6, comments = concat(orderMainNo, ',', orderDetailNo);
            }
        }
    }
    ACCEPT return {
        /*
        订单退货积分计算
        */
        FOR detail {
            var lReTotalPoint int;        -- 本次退货总积分
            var pointYear int;          -- 积分年度
            var customer int;           -- 最终积分用户
            var rTotalPoint int;        -- 订单总积分
            var rPoint int;
            var orderMainNo char(50);
            var orderDetailNo char(50);
            var orderDetailId int;
            var multiple int;
            var rReTotalPoint int;      -- 退货总积分上次值
            var rRePoint int;           -- 退货有效积分上次值

            var dReTotalPoint int;          --总积分差
            var dRePoint int;          --有效积分差

            set orderDetailId = id;

            set orderMainNo = a.orderMainNo
            from OrderDetail as a
            where a.id = [order-detail-id];
            set orderMainNo = ifnull(orderMainNo,'');

            -- 退货积分计算、获取与退货订单关联订单积分
            set lReTotalPoint = -amount * a.multiple, rTotalPoint = a.totalPoint, rPoint = a.point, pointYear = year(b.createDate), customer = b.endUser
            from DxOrderDetail as a
                 join OrderDetail as b on a.id = b.id
            where a.id = [order-detail-id];
            set lReTotalPoint = ifnull(lReTotalPoint, 0), rTotalPoint = ifnull(rTotalPoint, 0), rPoint = ifnull(rPoint, 0);

            -- 获取退货积分上次值
            set rReTotalPoint = a.totalPoint, rRePoint = a.point
            from DxReOrderDetail as a
            where a.id=id;
            set rReTotalPoint = ifnull(rReTotalPoint, 0), rRePoint = ifnull(rRePoint, 0);

            -- 退货总积分，记账、历史记录
            set dReTotalPoint = lReTotalPoint - rReTotalPoint;
            if ( dReTotalPoint <> 0 ) {
                -- 1.退货积分记账
                -- 1.1. 针对客户
                book PointBookByCustomer at (customer, pointYear) set totalpoint += -dReTotalPoint;
                -- 1.2.退货订单积分记账，针对订单明细
                WITH DxReOrderDetail as a ID = orderDetailId set a.totalpoint += dReTotalPoint;

                -- 2.积分历史记录
                -- 2.1针对客户、年度、积分、积分来源分类、积分源ID(订单ID,积分券Id),备注（上次积分与本次积分计算过程）
                history PointHistoryByCustomer 
                    set pointYear = pointYear, point = dReTotalPoint, source = 5, comments = concat(orderMainNo, ',', '退货'), customer = customer;               
                -- 2.2 针对订单明细
                history ReOrderDetailPointHistory
                    set orderDetailId = orderDetailId, pointYear = pointYear, point = dReTotalPoint, type = 5, comments = concat(orderMainNo, ',', '退货');                
            }

            -- 退货有效积分，记账、历史记录
            set dRePoint = lReTotalPoint - rRePoint;
            if ( dRePoint <> 0 ) {
                -- 1.退货积分记账
                -- 1.1. 针对客户
                book PointBookByCustomer at (customer, pointYear) set point += -dRePoint;
                -- 1.2.退货订单积分记账，针对订单明细
                WITH DxReOrderDetail as a ID = orderDetailId set a.point += dRePoint;

                -- 2.积分历史记录
                -- 2.1针对客户、年度、积分、积分来源分类、积分源ID(订单ID,积分券Id),备注（上次积分与本次积分计算过程）
                history PointHistoryByCustomer 
                    set pointYear = pointYear, point = dRePoint, source = 6, 
                    comments = concat(orderMainNo, ',', '退货'), customer = customer;               
                -- 2.2 针对订单明细
                history ReOrderDetailPointHistory
                    set orderDetailId = orderDetailId, pointYear = pointYear, point = dRePoint, 
                    type = 6, comments = concat(orderMainNo, ',', '退货');                
            }
        }
    };



BUS ps2Bus from [百灵威系统工程部]/pointShop2
ACCEPT pointInitialization {
    --订单积分
    if(source=5){
        
        book orderBack at(orderItemId) set orderId = orderId, customer = customer, 
            brand = brand, product = product, pack = pack, quantity = quantity, price = price,
            currency = currency, retail = retail, retailCurrency = retailCurrency, totalPoint += mscore,
            mark = mark, createDate = createDate;

        --总积分
        book PointBookByCustomer at (customer, pointYear) set totalpoint += mscore;
        history PointHistoryByCustomer 
            set pointYear = pointYear, point = mscore, source = 5, comments = comments, customer = customer;

        --有效积分
        if(isvalid=1){
            book PointBookByCustomer at (customer, pointYear) set point += mscore;
        }

    }else if(source=2){  --签到积分

        book PointBookByCustomer at (customer, pointYear) set point += mscore, totalpoint += mscore;
        history PointHistoryByCustomer 
            set pointYear = pointYear, point = mscore, source = 2, comments = comments, customer = customer; 

    }else if(source=3){  --积分兑换

        book PointBookByCustomer at (customer, pointYear) set usedPoint += mscore;
        history PointHistoryByCustomer 
            set pointYear = pointYear, point = (0-mscore), source = 3, comments = comments, customer = customer; 

    }
    /*
    if(isvalid=1){
        if exists (select 1 from PointBookByCustomer as e where e.customer=customer and e.pointYear=pointYear){
            book PointBookByCustomer at (customer, pointYear) set point += mscore;
        }
    }*/
};
