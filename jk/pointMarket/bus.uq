BUS pointShopBus from [百灵威系统工程部]/pointShop;
BUS pointShop2Bus from [百灵威系统工程部]/pointShop2;
BUS webUserBus from [百灵威系统工程部]/webUser;
bus couponDrawedBus from 百灵威系统工程部/coupon;

/*
 * 接收从内部系统导入的积分
*/
BUS pointBus from [百灵威系统工程部]/pointShop
    ACCEPT customerPoint 
    BUS webUserBus.getWuByCu as q into tb {
        set q.customer = customer;
    } {
        book PointBookByCustomer at (customer, pointYear) set point = point, totalPoint = totalPoint, usedPoint = usedPoint;

        -- 根据customer获取webuser
        var webUserid ID [$user];
        foreach tb {
            set webUserid = webUser;
        }
        -- 同时将临时积分清空
        if( not webUserid is null ){
            book PointBook at (webUserid, pointYear) set point = point, totalPoint = totalPoint, usedPoint = usedPoint,
            tempPoint = 0;
        }
    };

/*
 * webUser使用了credits后，删除该webUser领用的credits
*/
bus couponBus from 百灵威系统工程部/coupon
    ACCEPT creditsUsedByWebUser {
        var pointYear int;
        set pointYear = YEAR(NOW());
        delete a from WebUserCredits as a where a.webUser = webUser and a.credits = coupon;
        book WebUserCreditsUsed at (webUser, coupon, orderId ) set  usedDate = now();
        book PointBook at (webUser, pointYear) set tempPoint += point;
        history PointHistory
            set webUser = webuser, pointYear = pointYear, source = 1, point = point, comments = orderid;
    };

BUS webUserAuditPassBus from [百灵威系统工程部]/WebUser
    ACCEPT WebUserAuditPass
    {
        -- 审核后Customer的积分导入到webuser
        if(not exists(select 1 from PointBook as pb where pb.webUser = webUser)) {
            foreach (
                var pointYear int, point int, usedPoint int ,totalPoint int
                of select a.pointYear, a.point , a.usedPoint, a.totalPoint
                from PointBookByCustomer as a where a.customer = customer
            ) {
                book PointBook at (webUser, pointYear) set  point = point, usedPoint = usedPoint, totalPoint = totalPoint;
            }
        }

        -- 把webuser领用的积分券导入内部
        bus couponDrawedBus.creditsDrawedByCustomer set customer = customer, webUser = webUser;
        foreach (
            var credits int, createDate date, expiredDate date 
            of select a.credits, a.createDate, a.expiredDate
            from WebUserCredits as a 
            where a.webUser = webUser and a.expiredDate > now()
        ) {
            bus couponDrawedBus.creditsDrawedByCustomer into coupons add coupon = credits, 
                createDate = createDate, expiredDate = expiredDate;
        }

        -- 审核后webUser的签到积分导入到内部
        foreach(
            var pointYear2 int, signInPoint int
            of  select pointYear2, sum(h.point) as signInPoint from PointHistory as h 
            where h.webUser = webUser and h.pointYear > year(now()) - 3 and h.source = 2
            group by h.pointYear as pointYear2
        ) {
            bus pointShop2Bus.customerSignIn set customer = customer, webUser = webUser, pointYear = pointYear2, point = signInPoint, comments = '';
        }
    };

/*
 * 在joint中发送bus，此处接收，用于将积分可兑换产品从老系统同步到tonva
 * 2020/11/10, 此bus作废不再使用
*/
BUS pointProductBus from [百灵威系统工程部]/pointShop
    ACCEPT pointProductBus {
        var pointProductLibId int;
        set pointProductLibId = pointProduct from PointProductSource where sourceId = pack and type = 'self';
        if (pointProductLibId is null) {
            TUID pointProductLib into pointProductLibId set description = description, descriptionC = descriptionC,
                grade = grade, point = point, startDate = from_unixtime(startDate), endDate = from_unixtime(endDate), imageUrl = imageUrl,
                isValid = isValid; 
            BOOK PointProductSource at(pointProductLibId, pack) set type = 'self';
        } else {
            TUID pointProductLib id(pointProductLibId) set description = description, descriptionC = descriptionC,
                grade = grade, point = point, startDate = from_unixtime(startDate), endDate = from_unixtime(endDate), imageUrl = imageUrl,
                isValid = isValid; 
        }
    };

BUS orderChangedBus from [百灵威系统工程部]/[orderChanged]
    ACCEPT orderChanged {
        var rTotalPoint int;
        set rTotalPoint = a.totalPoint
        from    OrderBack as a
        where   a.orderItemId = orderItemId;
    
        set rTotalPoint = ifnull(rTotalPoint, 0);
        /*
        var currentDate date, currentYear int, currentMonth int;
        set currentDate = date(now());
        set currentYear = year(currentDate), currentMonth = month(currentDate);
        
        var priceRMB dec(12, 2), retailRMB dec(12, 2);
        set priceRMB = price * a.RMBTo 
        from    CurrencyExchangeRate as a
        where   a.currency = currency and a.year = currentYear and a.month = currentMonth; 
        set retailRMB = retail * a.RMBTo 
        from    CurrencyExchangeRate as a
        where   a.currency = retailCurrency and a.year = currentYear and a.month = currentMonth; 

        set price = priceRMB, retail = retailRMB;
        */

        var dDiscount dec(12, 2);
        set dDiscount = b.discount
        from    BrandMinDiscount as b
        where   b.brand = brand;

        -- 计算积分差
        var nTotalPoint int, diffPoint int;
        if(mark = 'C' or (price < retail * ifnull(1 - dDiscount, 1))) {
            set nTotalPoint = 0;
        } else {
            set nTotalPoint = price * quantity * 1;
        }
        set diffPoint = nTotalPoint - rTotalPoint;

        var pointYear int;
        set pointYear = year(from_unixtime(createDate));
        if(diffPoint <> 0) {
            book PointBookByCustomer at (customer, pointYear) set totalpoint += diffPoint;
            history PointHistoryByCustomer 
                set pointYear = pointYear, point = diffPoint, source = 5, comments = orderItemId, customer = customer;
        }

        book orderBack at(orderItemId) set orderId = orderId, customer = customer, 
            brand = brand, product = product, pack = pack, quantity = quantity, price = price,
            currency = currency, retail = retail, retailCurrency = retailCurrency, totalPoint += diffPoint,
            mark = mark, createDate = from_unixtime(createDate);
    };

BUS orderPaiedBus from [百灵威系统工程部]/[FiReceivable]
    ACCEPT innerOrderPaid {
        /*
        -- 测试数据 orderItemId找不到时的固定值
        set orderItemId = 'LCW1742012231406135837527', amount = 744;
        */
        
        var totalPoint int, customer ID Customer,price dec(12, 2), quantity int;
        set totalPoint = a.totalPoint, price = a.price, quantity = a.quantity, customer = a.customer
        from    OrderBack as a
        where   a.orderItemId = orderItemId;

        /*
            -- 货币转换 暂时不实现
            var currentDate date, currentYear int, currentMonth int;
            set currentDate = date(now());
            set currentYear = year(currentDate), currentMonth = month(currentDate);
            
            var amountRMB dec(12, 2);
            set amountRMB = amount * a.RMBTo 
            from    CurrencyExchangeRate as a
            where   a.currency = currency and a.year = currentYear and a.month = currentMonth; 
            set amount = amountRMB;
        */
    
        var realPoint int, subVal int, pointYear int;
        set realPoint = price * quantity / totalPoint * amount;
        set pointYear = year(from_unixtime(createDate/1000));
        set subVal = 0;
        if(action <> 1) {
            set realPoint = realPoint * -1, subVal = realPoint;
        }
        book PointBookByCustomer at (customer, pointYear) set point += realPoint,totalPoint += subVal;
        history PointHistoryByCustomer 
            set pointYear = pointYear, point = realPoint, source = 6, comments = orderItemId, customer = customer;
   
    };