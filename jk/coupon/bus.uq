bus JkOrderBus from 百灵威系统工程部/[jk-order];

bus JkCustomerPool from 百灵威系统工程部/[jk-customerpool]
    ACCEPT [bound-strength-changed] {
        FOR detail {
            if(strength = EnumBoundStrength.strong){
                DELETE t from IxPoolCustomer as t where t.ix = 1 and t.xi = customer;
            }
        }
    };

bus cBus from 百灵威系统工程部/coupon
    ACCEPT [order-coupon] {
        if(coupon > 0){
            WITH IxCouponUsed ixx = coupon ix = couponUser xi = id set usedDate = now();

            VAR couponSender ID;
            SET couponSender = b.xi 
            from Coupon as t join IxWebuserEmployeeCopy as b on t.creator = b.ix
            where t.id = coupon;

            PROC ProcCustomerBound(couponUser, couponSender, EnumBoundType.coupon);
        }else{
            VAR currSalesman ID;
            SET currSalesman = t.salesman
            from  CustomerBound as t where t.id = couponUser; 
            if(currSalesman is not null){
                WITH CustomerBound as a id = couponUser SET a.salesman = currSalesman, a.boundDate = date(now()), a.boundDays = 90; 
                BUS JkCustomerPool.[bound-strength-changed] into detail 
                    ADD customer = couponUser, salesman = currSalesman, strength = EnumBoundStrength.strong;
                BUS JkCustomerPool.[bound-strength-changed] LOCAL;
            }
        }

        VAR currentBound ID, boundType ENUM EnumBoundType;
        SET currentBound = t.salesman, boundType = t.boundType
        from CustomerBound as t where t.id = couponUser;
        if(currentBound is not null){
            BUS JkOrderBus.[order-bound-staff-sales] set order = id;
            BUS JkOrderBus.[order-bound-staff-sales] into [to] 
                ADD [id] = currentBound, [bound-type] = 'assign';
            if(boundType = EnumBoundType.coupon){
                BUS JkOrderBus.[order-bound-staff-sales] into [to] 
                    ADD [id] = currentBound, [bound-type] = 'coupon';
            }
        }
    }
    -- 接收内部系统客户与销售的对应关系
    ACCEPT [customer-bounded] {
        PROC ProcCustomerBound(customer, salesman, boundType);
    };

BUS ps2Bus from [百灵威系统工程部]/PointShop2
    QUERY getCreditsMultiple {
        var multiple int;
        if exists(select 1 from coupon where id = credits and type = 2) {
            set multiple = 2;
        } else {
            set multiple = 0;
        }
        into bus select multiple;
    };