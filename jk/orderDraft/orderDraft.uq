TUID Seller (
    id,
    main name char(10) not null,
    search(name), 
);

BOOK Cart ver 0.1 (
    key staff ID [$user], 
    key user ID,
    key product ID ProductX not null,
    key pack OF product.PackX not null,
    price dec(12,2) not null,
    currency ID Currency not null,
    quantity int not null,
    date datetime not null,
);

SHEET OrderDraft (
    webUser ID WebUser,         -- 当前的webuser
    organization ID Organization,
    buyerAccount ID BuyerAccount,   -- webUser相关的buyerAccount

    ARR orderItems (
        product ID ProductX not null,
        pack OF product.PackX not null,
        price dec(12, 2) not null,          -- 最终的成交价（即折后价格）
        quantity int not null,
        subAmount dec(12, 2) not null,
        refrigerationFee dec(12, 2),
        freezingFee dec(12, 2),
        packingFee dec(12, 2),
        proxyFreightFee dec(12, 2),
    ),
    shippingContact ID Contact,
    invoiceContact ID Contact,

    invoiceType ID InvoiceType,
    invoiceInfo ID InvoiceInfo,

    amount dec(12, 2),          -- 订单明细中subamount汇总+运费+运费减免
    currency ID Currency,
    freightFee dec(12, 2),
    freightFeeRemitted dec(12, 2),
    coupon ID Coupon,
    couponOffsetAmount dec(12, 2),
    couponRemitted dec(12,2),
    point int,
    comments char(1000),
    seller ID Seller,
    salesRegion ID SalesRegion,
    orderMaker ID [$user],
)
ACTION submit {
    history OrderDraftHistory set orderDraft = $id, webUser = webUser, organization = organization, buyerAccount = buyerAccount,
        shippingContact = shippingContact, invoiceContact = invoiceContact, invoiceType = invoiceType, InvoiceInfo = invoiceInfo,
        amount = amount, currency = currency, freightFee = freightFee, freightFeeRemitted = freightFeeRemitted,
        coupon = coupon, couponOffsetAmount = couponOffsetAmount, point = point, comments = comments,
        seller = seller, salesRegion = salesRegion, orderMaker = orderMaker;
    foreach orderItems {
        history OrderDraftItemHistory
            set product = product, pack = pack, price = price, quantity = quantity, amount = subAmount, currency = currency,
                freezingFee = freezingFee, refrigerationFee = refrigerationFee, packingFee = packingFee, proxyFreightFee = proxyFreightFee;
    }
    STATE TO Sending;
}
STATE Sending {
    Action SendOut {
        HISTORY OrderDraftAction set id = $id, action = 1, operator = $user;
        STATE TO Sended;
    }
}
STATE Sended {
    ACTION Open {
        HISTORY OrderDraftAction set id = $id, action = 2, operator = $user;
        STATE TO BeingReviewed;
    } 
    ACTION SendOut {
        HISTORY OrderDraftAction set id = $id, action = 1, operator = $user;
    }
}
STATE BeingReviewed {
    Action Pass {
        HISTORY OrderDraftAction set id = $id, action = 3, operator = $user;
        STATE TO END;
    }
    ACTION Cancel {
        HISTORY OrderDraftAction set id = $id, action = 4, operator = $user;
        STATE TO Canceled;
    }
    ACTION SendOut {
        HISTORY OrderDraftAction set id = $id, action = 1, operator = $user;
    }
}
STATE Canceled{
    
};

HISTORY OrderDraftHistory (
    date,
    orderDraft bigint,
    webUser ID WebUser,         -- 当前的webuser
    organization ID Organization,
    buyerAccount ID BuyerAccount,   -- webUser相关的buyerAccount

    shippingContact ID Contact,
    invoiceContact ID Contact,

    invoiceType ID InvoiceType,
    invoiceInfo ID InvoiceInfo,

    amount dec(12, 2),          -- 订单明细中subamount汇总+运费+运费减免
    currency ID Currency,
    freightFee dec(12, 2),
    freightFeeRemitted dec(12, 2),
    coupon ID Coupon,
    couponOffsetAmount dec(12, 2),
    couponRemitted dec(12,2),
    point int,
    comments char(1000),
    seller ID Seller,
    salesRegion ID SalesRegion,
    orderMaker ID [$user],
);

HISTORY OrderDraftItemHistory (
    date,
    product ID ProductX,
    pack OF product.PackX,
    price dec(12, 2),
    quantity int,
    amount dec(12, 2),
    refrigerationFee dec(12, 2),
    freezingFee dec(12, 2),
    packingFee dec(12, 2),
    proxyFreightFee dec(12, 2),
    currency ID Currency,
    user,
    sheet,
    type,
    row,
);

HISTORY OrderDraftAction (
    date,
    id bigint,
    action smallint,
    operator ID [$user]
);