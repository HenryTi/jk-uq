BUS orderbus ver 0.4 from [百灵威系统工程部]/order
    ACCEPT order (
        type, id as orderId, no, webuser, organization, customer as customerX, buyerAccount,
        shippingContact, invoiceContact, invoiceType, invoiceInfo,
        freightFee, freightFeeRemitted,
        amount, currency,
        coupon, couponOffsetAmount, couponRemitted, point, comments, salesRegion as salesRegionX
    )
    arr orderItems (
        product, pack, quantity, price, subAmount
    )
    BUS pointBus.getRetailPrice into tb {
        set customer = 0;
        foreach orderItems {
            into packs select pack as packid, salesRegionX as salesRegion;
        }
    }{
        -- 注：type  1:表示做合同有customer的  2：表示做合同没有customer 3：表示审核有customer
        -- step1. 获取优惠码所属销售和类型，没有优惠码获取客户当前轻代理和关联类型
        var couponID ID Coupon, order char(50), code bigint, sales ID [$user], mycustomer ID MyCustomer, types char(20);
        if(coupon > 0){
            set     sales = c.user, --轻代理
                    couponID = c.id, types = c.types
            from    Coupon as c
            where   c.id = coupon;
        }else if (not customerX is null){
            set     sales = a.sales, types = a.types 
            from    CustomerNowSalesBOOK as a 
            where   a.customer = customerX and a.sales <> 0;
        }

        -- step.2.绑定关系或者更新关系
        if(couponID > 0){
            -- step.2.1.首先要记录优惠码的使用记录
            BOOK CouponOrderMap at (coupon, orderId, webuser) set  createDate = NOW();

            if (type in(1, 3) and not customerX is null){
                -- step.2.2.绑定当前轻代理，或者延期当前轻代理。
                BOOK CustomerNowSalesBOOK at (customerX) 
                    set sales = sales, validity = dateadd(month, 3, NOW()), types = types, webuser = webuser;
                -- step.2.3.记录历史
                history CustomerNowSalesHistory 
                    set customer = customerX, webuser = webuser, sales = sales, validity = dateadd(month, 3, NOW()), types = types;
                -- step.2.4.发送BUS到内部系统，同步记录客户/销售的绑定关系
                bus salesTaskBus.assistCustomerNowSales set sales = sales, customer = customerX, webuser = webuser, createdate = NOW();
            }
        }

        -- step.3.记录销售记录(产品明细)
        if(sales > 0){
            -- 计算合同的总代理价
            var sumAgentprice dec(12, 2);
            set sumAgentprice = 0;
            foreach orderItems {
                var agentprices dec(12, 2), discount dec(12, 4);
                set agentprices = null;
                foreach tb.retailPriceResult {
                    if(pack = packid){
                        set discount = 0;
                        set discount = a.discount  from BottomDiscount as a where a.brand = brand;
                        set agentprices = retailPrice * ( 1 - discount );
                        set agentprices = case when price < agentprices then price else agentprices end;
                    }
                }
                -- 如果没有获取代理价则代理价等于销售价格
                if(not agentprices is null){
                    set sumAgentprice = sumAgentprice + ( agentprices * quantity);
                }else{
                    set sumAgentprice = sumAgentprice + subAmount;
                }
            }

            -- 记录客户订单
            var pid ID;
            if(type in (1, 2)){
                if not exists(select 1 from WebUserEmployeeMap as a where a.webuser = sales){
                   PENDING CustomerOrderPending + ( sales:sales, webuser:webuser, order:orderId, orderNo:no, price:amount, agentprices:sumAgentprice, types:types ) to pid;
                }
            } 

            -- 创建新客户(好像是在界面上会显示“新客户”，这个有什么作用?)
            if(type in (1, 3) and not customerX is null){
                set mycustomer = a.mycustomer from CustomerMyCustomerMap as a where a.customer = customerX limit 1;
                if(mycustomer is null){
                    BOOK NewMyCustomerBook at(sales, customerX) set webuser = webuser, createTime = NOW(); --创建新客户
                }
            }
        }
    };


BUS psb ver 0.4 from [百灵威系统工程部]/coupon
    ACCEPT creditsInnerMatched (
        orderId, customer, webUser, amount, currency, point, coupon 
    )
    arr orderItems (
        row, orderItemId, point
    ){
        --step.1.获取优惠码所属销售和类型， 没有优惠码获取客户当前轻代理和关联类型
        var couponID ID Coupon, order char(50), sales ID [$user], types char(20);
        if(coupon > 0){
            set     sales=c.user, --轻代理
                    couponID = c.id,
                    types = c.types
            from    Coupon as c
            where   c.id = coupon and c.types = 'credits';
        } else if(not customer is null){
            set     sales = a.sales, types = a.types 
            from    CustomerNowSalesBOOK as a 
            where   a.customer = customer and a.types='credits';
        }

        if(couponID > 0){
            --step.2.1.首先要记录积分码的使用记录
            BOOK CouponOrderMap at ( coupon, 1, webUser ) set  createDate = NOW(); ; --添加积分码使用记录
            --step.2.2.绑定当前轻代理，或者延期当前轻代理。
            BOOK CustomerNowSalesBOOK at (customer) set sales = sales, validity = dateadd(month, 3, NOW()), 
                types = 'credits', webUser = webUser; 
            --step.2.3.记录历史
            history CustomerNowSalesHistory set customer = customer, webuser = webUser, sales = sales, 
                validity = dateadd(month, 3, NOW()), types = types;
            --step.2.4.发送BUS到内部系统
            bus salesTaskBus.assistCustomerNowSales set sales=sales, customer = customer, webuser= webUser, createdate = NOW();
        }
        
        if( sales > 0 ){
            if not exists( select 1 from WebUserEmployeeMap as a where a.webuser = sales ){
                var pid ID;
                PENDING CustomerOrderPending + ( sales:sales, webuser:webUser, order:1, orderNo:orderId, price:amount, agentprices:amount, types:'credits' ) to pid;
            }
        }
    };

 
BUS assistAchievementbus ver 0.4 from [百灵威系统工程部]/salestask
    ACCEPT assistAchievement (
       sales, salesAmount, amount, receivableAmount, withdrawalAmount 
    ){
        BOOK SalesAchievementBook at( sales ) set Level = 1, oneSaleVolume = salesAmount, oneAchievement = amount
            , totalReceivableAmount = receivableAmount, waitWithdrawal = withdrawalAmount;
    };
  
BUS assistAchievementHistorybus ver 0.4 from [百灵威系统工程部]/salestask
    ACCEPT assistAchievementHistory (
       sales, webuser, orderNo, salesAmount, amount
    ){

        History AchievementHistory set sales = sales, webuser = webuser, orderNo = orderNo, price = salesAmount, agentprices = salesAmount, types = 1, amount = amount;
    };



/**
优惠码使用后:
1、首先要记录优惠码的使用记录
    获取订单中的 订单编号 和 优惠码 记录下来;

2、绑定当前轻代理，或者延期当前轻代理。

3、绑定客户资料和客户档案的关系
    客户订单的 webuser 和 优惠码 的内部客户编号进行关联

4、记录业绩（ 产品明细 ）
    记录销售的业绩；销售业绩为 （成交价 - 代理价） * 0.15
    记录销售上一级的业绩；上一级业绩为 （成交价 - 代理价） * 0.02

5、无优惠券如果有当前轻代理也要记录在轻代理明细名下

*/