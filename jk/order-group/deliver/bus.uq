BUS JkOrderBus ver 1.3 from 百灵威系统工程部/[jk-order]
ACCEPT order {
	VAR mainId ID;
	SET mainId = id;
	WITH OrderMain as a ID=id SET a.no=no, a.customer=customer;
	FOR detail {
		WITH OrderDetail as a 
			ID = id
			SET a.main=mainId
				, a.item=item, a.product=product
				, a.quantity=quantity, a.amount=amount, a.price=price;
	}
}
ACCEPT deliver {
	VAR customer ID;
	SET customer = a.customer FROM OrderMain as a WHERE a.id=id;
	
	-- 可以考虑在此处接受到发货信息后，把相同的收货信息合并新增为一个contact Id，ordermian中保存此contact
	WITH OrderMain as a ID=id SET a.contact=contact,
		 a.consigneename = consigneename, a.consigneeUnit = consigneeUnit,
		 a.consigneeMobile = consigneeMobile, a.consigneeTelphone = consigneeTelphone,
		 a.consigneeAddress = consigneeAddress, a.consigneeEmail = consigneeEmail;
	FOR detail {
		VAR quantity DEC(12,2);
		WITH OrderDetail as a ID=id SET a.warehouse=warehouse;
		SET quantity = a.quantity FROM OrderDetail as a WHERE a.id=id;
		WITH IxPendingDeliver ixx=warehouse ix=customer xi=id;
		WITH DxOrderDetail as a ID=id SET a.deliverShould=quantity;
		FOR (VAR user ID OF SELECT ix as user FROM IxUserWarehouse WHERE xi=warehouse) {
			TUID [$User] ID (user) SET poke=1;
		}
	}
	LOG 'JKOrder-deliver SCHEDULE' SUBJECT 'delay 1';
	SCHEDULE AutoWarehouseDeliver DELAY 1; -- 1分钟以后执行一次
}
ACCEPT [deliver-ext] {
	FOR detail {
		WITH OrderDetailX as a ID=id SET a.needInsuredWhenDelivery=needInsuredWhenDelivery,
		     a.expresslogistics = expresslogistics, a.coaquantity = coaquantity,
             a.msdsquantity = msdsquantity, a.receiptquantity = receiptquantity,
             a.purchasebillquantity = purchasebillquantity, a.showprice = showprice,
             a.appointlot = appointlot, a.deliverynotes = deliverynotes, a.outboundreason = outboundreason;
	}
}
ACCEPT [done-pickup] {
	FOR detail {
		VAR main INT;
		SET main=a.main FROM DeliverDetail as a WHERE a.id=id;
		WITH DxOrderDetail as a ID=id SET a.pickDone=value;
		WITH DxDeliver as a ID=main SET a.pickRows=a.pickRows+1;
	}
}
ACCEPT [return] {
	VAR customer ID;
	SET customer = a.customer FROM OrderMain as a WHERE a.id=id;
	FOR detail {
		VAR deliverShould DEC(12,2), deliverDone DEC(12,2), returnDone DEC(12,2), retQuantity DEC(12,2)
			, warehouse ID;
		SET deliverShould=IFNULL(a.deliverShould, 0)
			, deliverDone=IFNULL(a.deliverDone, 0)
			, returnDone=IFNULL(a.returnDone, 0)
			FROM DxOrderDetail as a WHERE a.id=[order-detail-id];
		SET warehouse=a.warehouse FROM OrderDetail as a WHERE a.id=[order-detail-id];
		WITH DxReturnDetail as a ID=id SET a.orderDetail=[order-detail-id], a.quantity=quantity;
		-- 做return相关操作
		IF exists(SELECT xi FROM IxPendingDeliver WHERE ixx=warehouse AND ix=customer AND xi=id) {
			SET retQuantity = quantity;
			WITH DxOrderDetail ID=id SET returnDone=retQuantity;
			IF deliverShould - deliverDone - returnDone - retQuantity <= 0 {
				WITH IxPendingDeliver IXX=warehouse IX=customer XI=id DEL;
				WITH DxReturnDetail as a ID=id SET a.quantityDone=retQuantity;
			}
		}
		ELSE {
			WITH IxCustomerPendingReturn ix=customer xi=id;
			SET retQuantity = 0;
		}
		Bus JkOrderBus.[done-return-deliver] INTO detail
			ADD id=id, value=retQuantity;
	}
};
