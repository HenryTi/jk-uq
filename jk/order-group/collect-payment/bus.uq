BUS JkOrderBus ver 0.7 from 百灵威系统工程部/[jk-order]
ACCEPT order {
	VAR mainId ID, row INT;
	SET mainId = id;
	WITH OrderMain as a ID=id SET a.no=no, a.customer=customer;
	SET row = 1;
	FOR detail {
		WITH OrderDetail as a 
			ID = id
			SET a.main=mainId, a.row=row
				, a.item=item, a.product=product
				, a.quantity=quantity, a.amount=amount, a.price=price;
		SET row=row+1;
	}
}
ACCEPT receive {
	FOR detail {
		VAR customer ID;
		SET customer = b.customer 
			FROM OrderDetail as a JOIN OrderMain as b ON a.main=b.id
			WHERE a.id=id;
		WITH IxCustomerPendingReceive IX=customer XI=id;
		WITH DxOrderDetail as a ID=id SET a.receive=value;
	}
}
ACCEPT invoice {
	FOR detail {
		VAR customer ID;
		SET customer = b.customer 
			FROM OrderDetail as a JOIN OrderMain as b ON a.main=b.id
			WHERE a.id=id;
		WITH IxCustomerPendingInvoice IX=customer XI=id;
		WITH DxOrderDetail as a ID=id SET a.invoice=value;
	}
}
ACCEPT [return] {
	VAR customer ID;
	SET customer = a.customer FROM OrderMain as a WHERE a.id=id;
	FOR detail {
		VAR retReceive DEC(12,2);
		VAR receive DEC(12,2), receiveDone DEC(12,2), receiveReturnDone DEC(12,2);
		VAR retInvoice DEC(12,2);
		VAR invoice DEC(12,2), invoiceDone DEC(12,2), invoiceReturnDone DEC(12,2);
		SET receive=IFNULL(a.receive, 0)
			, receiveDone=IFNULL(a.receiveDone, 0)
			, receiveReturnDone=IFNULL(a.receiveReturnDone, 0)
			, invoice=IFNULL(a.invoice, 0)
			, invoiceDone=IFNULL(a.invoiceDone, 0)
			, invoiceReturnDone=IFNULL(a.invoiceReturnDone, 0)
			FROM DxOrderDetail as a WHERE a.id=[order-detail-id];
		WITH DxReturnDetail as a ID=id SET a.orderDetail=[order-detail-id]
			, a.receive=amount
			, a.invoice=amount;

		-- receive return相关操作
		IF exists(SELECT xi FROM IxCustomerPendingReceive WHERE ix=customer AND xi=id) {
			SET retReceive = amount;
			WITH DxOrderDetail ID=id SET receiveReturnDone=retReceive;
			IF receive - receiveDone - receiveReturnDone- retReceive <= 0 {
				WITH IxCustomerPendingReceive IX=customer XI=id DEL;
				WITH DxReturnDetail as a ID=id SET a.receiveDone=retReceive;
			}
		}
		ELSE {
			WITH IxCustomerPendingReceiveReturn ix=customer xi=id;
			SET retReceive = 0;
		}
		Bus JkOrderBus.[done-return-receive] INTO detail
			ADD id=id, value=retReceive;

		-- invoice return相关操作
		IF exists(SELECT xi FROM IxCustomerPendingInvoice WHERE ix=customer AND xi=id) {
			SET retInvoice = amount;
			WITH DxOrderDetail ID=id SET invoiceReturnDone=retInvoice;
			IF invoice - invoiceDone - invoiceReturnDone- retInvoice <= 0 {
				WITH IxCustomerPendingInvoice IX=customer XI=id DEL;
				WITH DxReturnDetail as a ID=id SET a.invoiceDone=retInvoice;
			}
		}
		ELSE {
			WITH IxCustomerPendingInvoiceReturn ix=customer xi=id;
			SET retInvoice = 0;
		}
		Bus JkOrderBus.[done-return-invoice] INTO detail
			ADD id=id, value=retInvoice;
	}
};
