BUS JkOrderBus ver 0.7 from 百灵威系统工程部/[jk-order]
ACCEPT order {
	VAR mainId ID, row INT;
	SET mainId = id;
	WITH OrderMain as a ID=id SET a.no=no, a.customer=customer;
	FOR detail {
		WITH OrderDetail as a 
			ID = id
			SET a.main=mainId
				, a.item=item, a.product=product
				, a.quantity=quantity, a.amount=amount, a.price=price;
	}
}
ACCEPT receive {
	FOR detail {
		VAR customer ID;
		SET customer = b.customer 
			FROM OrderDetail as a JOIN OrderMain as b ON a.main=b.id
			WHERE a.id=id;
		WITH IxCustomerPendingReceive IX=customer XI=id;
		WITH DxOrderDetail as a ID=id SET a.receive=value;
		WITH DxCustomerReceive ID=customer SET sumAmount+=value;
	}
}
ACCEPT invoice {
	FOR detail {
		VAR customer ID;
		SET customer = b.customer 
			FROM OrderDetail as a JOIN OrderMain as b ON a.main=b.id
			WHERE a.id=id;
		WITH IxCustomerPendingInvoice IX=customer XI=id;
		WITH DxOrderDetail as a ID=id SET a.invoice=value;
		WITH DxCustomerInvoice ID=customer SET sumAmount+=value;
	}
}
ACCEPT [return] {
	FOR detail {
		VAR customer ID;
		SET customer=b.customer 
			FROM OrderDetail as a JOIN OrderMain as b ON a.main=b.id
			WHERE a.id=[order-detail-id];
		WITH ReturnDetail as a ID=id SET a.main=id
			, a.orderDetail=[order-detail-id]
			, a.quantity=quantity
			, a.amount=amount;
		WITH DxOrderDetail as a ID=[order-detail-id] SET a.receiveReturn += amount
			, a.invoiceReturn += amount;
		WITH DxCustomerReceive ID=customer SET sumAmount -= amount;
		WITH DxCustomerInvoice ID=customer SET sumAmount -= amount;
	}
};
